apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"

ext {
    glide_version = "4.11.0"
    lifecycle_version = "2.2.0"
}

def versionPropsFile = rootProject.file("version.properties")
def versionBuild

android {
    compileSdkVersion 30

    buildToolsVersion = '30.0.2'

    /* Setting default value for versionBuild which is the last incremented value stored in the file */
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps["VERSION_BUILD"].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }

    /* Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk */
    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps["VERSION_BUILD"].toInteger() + 1
            versionProps["VERSION_BUILD"] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }

    defaultConfig {
        applicationId "com.mendelin.catpedia"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode versionBuild
        versionName "0.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        dexOptions {
            javaMaxHeapSize "4g"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "catpedia_${variant.buildType.name}_${defaultConfig.versionName}(${defaultConfig.versionCode}).apk"
        }
    }

    kapt {
        generateStubs = true
    }

    buildFeatures {
        dataBinding = true
    }

    buildTypes {
        release {
            debuggable true
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    // Hook to check if the release/debug task is among the tasks to be executed.
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleRelease)) { /* when run release task */
            autoIncrementBuildNumber()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude "META-INF/proguard/androidx-annotations.pro"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    /* Kotlin */
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"

    /* Android libraries */
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.3.0-alpha02"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0"

    /* Navigation components */
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    /* LiveData, ViewModel */
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    /* Testing */
    implementation "androidx.annotation:annotation:1.1.0"
    testImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test:runner:1.3.0-rc03"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0-rc03"

    /* Gson */
    implementation "com.google.code.gson:gson:2.8.6"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.9.3"

    // Retrofit with Moshi Converter
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    /* Retrofit networking library */
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    //implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    /* Timber logging */
    implementation "com.jakewharton.timber:timber:4.7.1"

    /* Image handling */
    implementation "com.github.bumptech.glide:glide:$glide_version"
}
